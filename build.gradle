plugins {
    id 'java'
    id 'maven-publish'
    id 'com.modrinth.minotaur' version '2.+'
    id 'fabric-loom' version "$fabricLoomVersion"
}

defaultTasks 'build'

project.group = 'net.pl3x.lib'
project.version = "$pl3xlibVersion"

repositories {
    maven { url = 'https://maven.fabricmc.net/' }
    maven { url = 'https://maven.terraformersmc.com/' }
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraftVersion"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:$fabricLoaderVersion"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabricApiVersion"
    modImplementation "com.terraformersmc:modmenu:${modmenuVersion}"
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    archiveBaseName = "${project.name}"
}

compileJava {
    options.encoding = 'UTF-8'
    options.release.set(17)
}

javadoc {
    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
    title = "${rootProject.name}-${project.version} API"
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching('fabric.mod.json') {
        expand(
                'version': project.version,
                'fabricApiVersion': fabricApiVersion,
                'fabricLoaderVersion': fabricLoaderVersion,
                'minecraftVersion': minecraftVersion,
        )
    }
}

loom {
    mixin {
        defaultRefmapName = 'pl3xlib.refmap.json'
    }
    accessWidenerPath = file('src/main/resources/pl3xlib.accesswidener')
    runConfigs.configureEach {
        ideConfigGenerated(true)
    }
}

publishing {
    repositories {
        maven {
            name = 'public'
            url = uri('https://repo.pl3x.net/public')
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "net.pl3x.lib"
            artifactId = "pl3xlib"
            version = "${project.version}"
            from components.java
        }
    }
}

modrinth {
    autoAddDependsOn = false
    token = System.getenv('MODRINTH_TOKEN')
    projectId = 'pl3xlib'
    versionName = "${project.version}"
    versionNumber = "${project.version}"
    versionType = 'release'
    uploadFile = rootProject.layout.buildDirectory.file("libs/${rootProject.name}-${project.version}.jar").get()
    gameVersions.addAll(["${minecraftVersion}"])
    loaders.addAll(['fabric'])
    changelog = System.getenv('COMMIT_MESSAGE')
}
